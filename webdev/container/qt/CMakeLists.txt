project(webdev-qt)

set(QT_COMPONENTS Qml WebView)
find_package(QT NAMES Qt6 Qt5 COMPONENTS ${QT_COMPONENTS} QUIET)
find_package(Qt${QT_VERSION_MAJOR} COMPONENTS ${QT_COMPONENTS} QUIET)

set(LIB_ALL_FOUND ON)
foreach (QT_COMPONENT ${QT_COMPONENTS})
    if (NOT Qt${QT_VERSION_MAJOR}${QT_COMPONENT}_FOUND)
        set(LIB_ALL_FOUND OFF)
        message(WARNING Qt${QT_VERSION_MAJOR}${QT_COMPONENT} " not found in " ${QT_DIR})
    endif ()
endforeach (QT_COMPONENT ${QT_COMPONENTS})

if (NOT ${LIB_ALL_FOUND})
    message(WARNING "code in " ${CMAKE_CURRENT_SOURCE_DIR} " will not be built")
else ()
    set(CMAKE_AUTORCC ON)

    if (ANDROID)
        set(ANDROID_MIN_SDK_VERSION 21)
        set(ANDROID_PACKAGE_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/android CACHE INTERNAL "")
        set(JS_QRC_DIR ${ANDROID_PACKAGE_SOURCE_DIR}/assets)
    else ()
        set(JS_QRC_DIR ${CMAKE_CURRENT_BINARY_DIR}/js)
    endif ()
    set(JS_BUILD_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../../webdev/build)
    file(GLOB_RECURSE JS_FILE_LIST ${JS_BUILD_DIR}/**)
    set(JS_QRC "")
    string(APPEND JS_QRC "<RCC>\n<qresource prefix=\"/\">\n")
    foreach (JS_FILE ${JS_FILE_LIST})
        message(STATUS "FILE=" ${JS_FILE})
        string(REPLACE ${JS_BUILD_DIR} "" JS_QRC_PATH ${JS_FILE})
        string(REGEX REPLACE "^(.*)/([^/]*)$" "\\1" JS_QRC_DEST ${JS_QRC_DIR}/${JS_QRC_PATH})
        #        message(STATUS "JS_QRC_DEST=" ${JS_QRC_DEST})
        file(COPY ${JS_FILE} DESTINATION ${JS_QRC_DEST})
        string(APPEND JS_QRC "<file alias=\"${JS_QRC_PATH}\">.${JS_QRC_PATH}</file>\n")
    endforeach ()
    string(APPEND JS_QRC "</qresource>\n</RCC>")
    if (NOT ANDROID)
        set(JS_QRC_FILE ${JS_QRC_DIR}/js.qrc)
        file(WRITE ${JS_QRC_FILE} ${JS_QRC})
        qt5_add_big_resources(JS_QRC_RES_FILE ${JS_QRC_FILE})
    endif ()
    set(RES_SRCS main.qrc)
    if (ANDROID)
        add_library(${PROJECT_NAME} SHARED main.cpp ${RES_SRCS} ${ANDROID_PACKAGE_SOURCE_DIR}/AndroidManifest.xml)
    else ()
        add_executable(${PROJECT_NAME} main.cpp ${RES_SRCS} ${JS_QRC_RES_FILE})
    endif ()

    set_target_properties(${PROJECT_NAME} PROPERTIES DEBUG_POSTFIX "d")

    set(QT_COMPONENT_TARGETS)
    foreach (QT_COMPONENT ${QT_COMPONENTS})
        list(APPEND QT_COMPONENT_TARGETS Qt${QT_VERSION_MAJOR}::${QT_COMPONENT})
    endforeach (QT_COMPONENT ${QT_COMPONENTS})

    target_link_libraries(${PROJECT_NAME} PRIVATE ${QT_COMPONENT_TARGETS})
endif ()
